---
layout: page
title: "H-Levels Lemmas"
category: types lemmas
toc: true
agda: true
gallery: true
latex: true
references: false
linkify: true
showcitation: true
---

<div class="hide" >
\begin{code}
{-# OPTIONS --without-K #-}
module _ where

open import TransportLemmas
open import EquivalenceType

open import ProductIdentities
open import CoproductIdentities

open import HomotopyType
open import HomotopyLemmas

open import HalfAdjointType
open import QuasiinverseType
open import QuasiinverseLemmas

open import FunExtAxiom
open import UnivalenceAxiom
open import HLevelTypes
\end{code}
</div>

## HLevel Lemmas

The following lemmas are not exactly in some coherent order.
We are planning to fix that issue.

### Proposition and Set lemmas

\begin{code}
module HLevelLemmas where
\end{code}

For any type, $$A : \Type$$,

{: .equation }
  $$ \isContr{A} â‡’ \isProp{A} â‡’ \isSet{A} â‡’ \mathsf{isGroupoid}{A}.$$

Contractible types are Propositions:
{: .foldable until="5"}
\begin{code}
  contrIsProp
    : {A : Type â„“}
    â†’ isContr A
    -----------
    â†’ isProp A

  contrIsProp (a , p) x y = ! (p x) Â· p y

  -- Synonyms
  isContrâ†’isProp = contrIsProp
\end{code}

To be contractible is itself a proposition.

\begin{code}
  contractible-from-inhabited-prop
    : {A : Type â„“}
    â†’ A
    â†’ isProp A
    ----------------
    â†’ Contractible A

  contractible-from-inhabited-prop a p = (a , p a )
\end{code}

Propositions are Sets:

{: .foldable until="5"}
\begin{code}
  propIsSet
    : âˆ€ {A : Type â„“}
    â†’ isProp A
    ----------
    â†’ isSet A

  propIsSet {A = A} f a _ p q = lemma p Â· inv (lemma q)
    where
      triang : {y z : A} {p : y == z} â†’ (f a y) Â· p == f a z
      triang {y}{p = idp} = inv (Â·-runit (f a y))

      lemma : {y z : A} (p : y == z) â†’ p == ! (f a y) Â· (f a z)
      lemma {y = y}{w} p =
        begin
          p                       ==âŸ¨ ap (_Â· p) (inv (Â·-linv (f a y))) âŸ©
          ! (f a y) Â· f a y Â· p   ==âŸ¨ Â·-assoc (! (f a y)) (f a y) p âŸ©
          ! (f a y) Â· (f a y Â· p) ==âŸ¨ ap (! (f a y) Â·_) triang âŸ©
          ! (f a y) Â· (f a w)
        âˆ
\end{code}

Synonyms:
\begin{code}
  prop-is-set  = propIsSet
  propâ†’set     = propIsSet
  isProp-isSet = propIsSet
  Prop-is-Set  = propIsSet
\end{code}


Propositions are Sets:
{: .foldable until="5"}
\begin{code}
  Set-is-Groupoid
    : {A : Type â„“}
    â†’ isSet A
    ----------
    â†’ isGroupoid A

  Set-is-Groupoid {A} A-is-set = Î» x y â†’ prop-is-set (A-is-set x y)
\end{code}


{: .foldable until="7"}
\begin{code}
  is-prop-A+B
    : {A : Type â„“áµ¢}{B : Type â„“â±¼}
    â†’ isProp A
    â†’ isProp B
    â†’ Â¬ (A Ã— B)
    ----------------
    â†’ isProp (A + B)

  is-prop-A+B ispropA ispropB Â¬AÃ—B (inl x) (inl xâ‚) = ap inl (ispropA x xâ‚)
  is-prop-A+B ispropA ispropB Â¬AÃ—B (inl x) (inr xâ‚) = âŠ¥-elim (Â¬AÃ—B (x , xâ‚))
  is-prop-A+B ispropA ispropB Â¬AÃ—B (inr x) (inl xâ‚) = âŠ¥-elim (Â¬AÃ—B (xâ‚ , x))
  is-prop-A+B ispropA ispropB Â¬AÃ—B (inr x) (inr xâ‚) = ap inr (ispropB x xâ‚)
\end{code}

Propositions are propositions. This time, please notice
the strong use of function extensionality, used twice here.

{: .foldable until="5"}
\begin{code}
  propIsProp
    : {A : Type â„“}
    -- (funext : Function-Extensionality)
    -------------------------------------
    â†’ isProp (isProp A)

  propIsProp {_}{A} =
    Î» x y â†’ funext (Î» a â†’
              funext (Î» b
                â†’ propIsSet x a b (x a b) (y a b)))
\end{code}

\begin{code}
  prop-is-prop-always = propIsProp
  prop-is-prop        = propIsProp
  propâ†’prop           = propIsProp
  isProp-isProp       = propIsProp
  is-prop-is-prop     = propIsProp
\end{code}

The dependent function type to proposition types is itself a
proposition.

{: .foldable until="6"}
\begin{code}
  isProp-pi
    : {A : Type â„“áµ¢} {B : A â†’ Type â„“â±¼}
    -- (funext : Function-Extensionality)
    â†’ ((a : A) â†’ isProp (B a))
    --------------------------
    â†’ isProp ((a : A) â†’ B a)

  isProp-pi props f g = funext Î» a â†’ props a (f a) (g a)
\end{code}

\begin{code}
  pi-is-prop = isProp-pi
  Î -isProp   = isProp-pi
  piIsProp   = isProp-pi
\end{code}

Propositional extensionality, here stated as `prop-ext`,
is a consequence of univalence axiom.

{: .foldable until="8"}
\begin{code}
  prop-ext
    : {A B : Type â„“}
    -- (ua : Univalence Axiom)
    â†’ isProp A
    â†’ isProp B
    â†’ (A â‡” B)
    -----------
    â†’ A == B

  prop-ext propA propB (f , g) =
    ua (qinv-â‰ƒ f (g , (Î» x â†’ propB _ _) , (Î» x â†’ propA _ _)))
\end{code}

Synomyms:

\begin{code}
  props-â‡”-to-== = prop-ext
  ispropA-B     = prop-ext
  propositional-extensionality = prop-ext
\end{code}

{: .foldable until="4"}
\begin{code}
  setIsProp
    : {A : Type â„“}
    -----------------
    â†’ isProp (isSet A)

  setIsProp {â„“} {A} pâ‚ pâ‚‚ =
    funext (Î» x â†’
      funext (Î» y â†’
        funext (Î» p â†’
          funext (Î» q â†’ propIsSet (pâ‚‚ x y) p q (pâ‚ x y p q) (pâ‚‚ x y p q)))))
\end{code}
\begin{code}
  set-is-prop = setIsProp
  setâ†’prop    = setIsProp
\end{code}

The product of propositions is itself a proposition.

{: .foldable until="6"}
\begin{code}
  isProp-prod
    : {A : Type â„“áµ¢} {B : Type â„“â±¼}
    â†’ isProp A
    â†’ isProp B
    ---------------------
    â†’ isProp (A Ã— B)

  isProp-prod p q x y = prodByComponents ((p _ _) , (q _ _))
\end{code}

\begin{code}
  Ã—-is-prop      = isProp-prod
  ispropAÃ—B      = isProp-prod
  Ã—-isProp       = isProp-prod
  propÃ—propâ†’prop = isProp-prod
\end{code}

{: .foldable until="5" }
\begin{code}
  isSet-prod
    : {A : Type â„“áµ¢} â†’ {B : Type â„“â±¼}
    â†’ isSet A â†’ isSet B
    -------------------
    â†’ isSet (A Ã— B)

  isSet-prod sa sb (a , b) (c , d) p q = begin
     p
      ==âŸ¨ inv (prodByCompInverse p) âŸ©
     prodByComponents (prodComponentwise p)
      ==âŸ¨ ap prodByComponents (prodByComponents (sa a c _ _ , sb b d _ _)) âŸ©
     prodByComponents (prodComponentwise q)
      ==âŸ¨ prodByCompInverse q âŸ©
     q
    âˆ
\end{code}
Synomys:

\begin{code}
  Ã—-is-set      = isSet-prod
  isSetAÃ—B      = isSet-prod
  Ã—-isSet       = isSet-prod
  setÃ—setâ†’set   = isSet-prod
\end{code}

\begin{code}
  Prop-/-â‰¡
    : {A : Type â„“áµ¢}
    â†’ (P : A â†’ hProp {â„“áµ¢})
    â†’ âˆ€ {aâ‚€ aâ‚} pâ‚€ pâ‚ {Î± : aâ‚€ â‰¡ aâ‚}
    ------------------------------
    â†’ pâ‚€ â‰¡ pâ‚ [ (# âˆ˜ P) / Î± ]

  Prop-/-â‰¡ P {aâ‚€} pâ‚€ pâ‚ {Î± = idp} = projâ‚‚ (P aâ‚€) pâ‚€ pâ‚
\end{code}

H-levels actually are preserved by products, coproducts, pi-types and sigma-types.


{: .foldable until="7"}
\begin{code}
  postulate
    +-of-sets-is-set
      : {A : Type â„“áµ¢}{B : Type â„“â±¼}
      â†’ isSet A
      â†’ isSet B
      ---------------
      â†’ isSet (A + B)

  {-
      The idea here is to have an iso between:
      A + B â‰ƒ âˆ‘ ğŸš P where P : (tt â†¦ A, ff â†¦ B)

        f : A + B â†’ âˆ‘ ğŸš P
           (left a) â†¦ (t , A)
  -}

  -- +-of-sets-is-set {â„“áµ¢} {â„“â±¼} {A} {B} issetA issetB x y p q = {!!}
  --   where
  --   f : (A + B) â†’ Type (â„“áµ¢ âŠ” â„“â±¼)
  --   f (inl a) = â†‘ â„“â±¼  A
  --   f (inr b) = â†‘ â„“áµ¢ B
  --
  --   aux
  --     : (a : A + B) â†’ f a
  --   aux (inl a) = Lift a
  --   aux (inr b) = Lift b
  --
  --   aux2 : (A + B) â†’ {!!}
  --   aux2 w
  --     with aux w
  --   ... | e = {!e!}
\end{code}



\begin{code}
  id-contractible-from-set
    : {A : Type â„“}
    â†’ isSet A
    â†’ {a a' : A}
    â†’ a â‰¡ a' â†’ isContr (a â‰¡ a')

  id-contractible-from-set iA {a}{.a} idp
    = idp , Î» q â†’ iA a a idp q
  -- This is quite obvious from the hset definition.
  -- But it's nice to spell it out fully.
\end{code}


Lemma 3.11.3: For any type A, `isContr A` is a mere proposition.

{: .foldable until="4"}
\begin{code}
  isContrIsProp
    : {A : Type â„“}
    --------------------
    â†’ isProp (isContr A)

  isContrIsProp {_} {A} (a , p) (b , q) =
    Î£-bycomponents (inv (q a) , isProp-pi (AisSet b) _ q)
      where
        AisSet : isSet A
        AisSet = propIsSet (contrIsProp (a , p))

  BookLemma3113 = isContrIsProp
\end{code}

Lemma 3.3.3 (HoTT-Book):
\begin{code}
  lemma333
    : {A : Type â„“áµ¢}{B : Type â„“â±¼}
    â†’ isProp A
    â†’ isProp B
    â†’ (A â†’ B)
    â†’ (B â†’ A)
    ----------
    â†’ A â‰ƒ B

  lemma333 iA iB f g = qinv-â‰ƒ f (g , gf , fg)
    where
    private
      fg : (f :> g) âˆ¼ id
      fg a = iA ((f :> g) a) a

      gf : (g :> f) âˆ¼ id
      gf b = iB ((g :> f) b) b

  BookLemma333 = lemma333
\end{code}

Lemma 3.3.2 (HoTT-Book):
\begin{code}
  prop-inhabited-â‰ƒğŸ™
    : {A : Type â„“}
    â†’ isProp A
    â†’ (a : A)
    ---------
    â†’ A â‰ƒ (ğŸ™ {â„“})
  prop-inhabited-â‰ƒğŸ™ iA a =
    lemma333 iA ğŸ™-is-prop (Î» _ â†’ unit) (Î» _ â†’ a)

  BookLemma332 = prop-inhabited-â‰ƒğŸ™
\end{code}

From Exercise 3.5 (HoTT-Book):
\begin{code}
  isProp-â‰ƒ-isContr
    : {A : Type â„“}
    â†’ isProp A â‰ƒ (A â†’ isContr A)

  isProp-â‰ƒ-isContr {A = A} =
    lemma333 isProp-isProp (pi-is-prop (Î» a â†’ isContrIsProp)) go back
    where
      private
        go : isProp A â†’ (A â†’ isContr A)
        go iA a = a , Î» a' â†’ iA a a'

        back : (A â†’ isContr A) â†’ isProp A
        back f = Î» a a' â†’ (! Ï€â‚‚ (f a) a) Â· (Ï€â‚‚ (f a) a')
\end{code}

Equivalence of two types is a proposition
Moreover, equivalences preserve propositions.

Contractible maps are propositions:

\begin{code}
  isContrMapIsProp
    : {A : Type â„“áµ¢} {B : Type â„“â±¼}
    â†’ (f : A â†’ B)
    -------------
    â†’ isProp (isContrMap f)

  isContrMapIsProp f = pi-is-prop (Î» a â†’ isContrIsProp)
\end{code}

\begin{code}
  isEquivIsProp
    : {A : Type â„“áµ¢}{B : Type â„“â±¼}
    â†’ (f : A â†’ B)
    â†’ isProp (isEquiv f)

  isEquivIsProp = isContrMapIsProp
\end{code}

Equality of same-morphism equivalences
{: .foldable until="6"}
\begin{code}
  sameEqv
    : {A : Type â„“áµ¢}{B : Type â„“â±¼}
    â†’ {Î± Î² : A â‰ƒ B}
    â†’ Ï€â‚ Î± == Ï€â‚ Î²
    â†’    Î± == Î²

  sameEqv {Î± = (f , Ïƒ)} {(g , Ï„)} p = Î£-bycomponents (p , (isEquivIsProp g _ Ï„))
\end{code}

{: .foldable until="6"}
\begin{code}
  equiv-iff-hprop
    : {A B : Type  â„“áµ¢}
    â†’ isProp A
    â†’ isProp B
    -----------------
    â†’ isProp (A â‰ƒ B)

  equiv-iff-hprop {A = A}{B} iA iB ef eg
    = sameEqv fâ‰¡g
    where
    private
      fâ‰¡g : (Ï€â‚ ef) â‰¡ (Ï€â‚ eg)
      fâ‰¡g = pi-is-prop (Î» _ â†’ iB) (Ï€â‚ ef) (Ï€â‚ eg)
\end{code}


{: .foldable until="6"}
\begin{code}
  propEqvIsprop
    : {A B : Type  â„“áµ¢}
    â†’ isProp A
    â†’ isProp B
    -----------------
    â†’ isProp (A == B)

  propEqvIsprop iA iB p q =
    begin
      p
        â‰¡âŸ¨ ! (ua-Î· p) âŸ©
      ua (idtoeqv p)
        â‰¡âŸ¨ ap ua (equiv-iff-hprop iA iB (idtoeqv p) (idtoeqv q)) âŸ©
      ua (idtoeqv q)
        â‰¡âŸ¨ ua-Î· q âŸ©
      q
     âˆ
\end{code}


Equivalences preserve propositions

{: .foldable until="6"}
\begin{code}
  isProp-â‰ƒ
    : {A : Type â„“áµ¢}{B : Type â„“â±¼}
    â†’ (A â‰ƒ B)
    â†’ isProp A
    ----------
    â†’ isProp B

  isProp-â‰ƒ eq prop x y =
    begin
      x                       ==âŸ¨ inv (lrmap-inverse eq) âŸ©
      lemap eq ((remap eq) x) ==âŸ¨ ap (Î» u â†’ lemap eq u) (prop _ _) âŸ©
      lemap eq ((remap eq) y) ==âŸ¨ lrmap-inverse eq âŸ©
      y
    âˆ
\end{code}

{: .foldable until="6" }
\begin{code}
  is-set-equiv-to-set
    : {A : Type  â„“áµ¢}{B : Type â„“â±¼}
    â†’ A â‰ƒ B
    â†’ isSet A
    ---------
    â†’ isSet B

  is-set-equiv-to-set {A = A}{B} e iA
    x y  =  isProp-â‰ƒ aux (iA (!f x) (!f y))
    where
    private
     f : A â†’ B
     f = lemap e

     !f : B â†’ A
     !f = remap e

     aux : (remap e x â‰¡ remap e y) â‰ƒ (x â‰¡ y)
     aux
       = qinv-â‰ƒ (Î» p â†’ ! (lrmap-inverse e) Â· ap f p Â· lrmap-inverse e)
                ((Î» { idp â†’ idp})
                , (Î» { idp â†’ Hâ‚})
                , Î» {p â†’ iA (!f x) (!f y) _ p})
       where
       Hâ‚ : (! lrmap-inverse e Â· idp) Â· lrmap-inverse e {x} == idp
       Hâ‚ = begin
         (! lrmap-inverse e Â· idp) Â· lrmap-inverse e
           â‰¡âŸ¨ ap (Î» w â†’ w Â· (lrmap-inverse e)) (! (Â·-runit _)) âŸ©
         ! lrmap-inverse e Â· lrmap-inverse e
           â‰¡âŸ¨ Â·-linv (lrmap-inverse e) âŸ©
         idp
         âˆ
  equiv-with-a-set-is-set = is-set-equiv-to-set
\end{code}

Above, we might want to use univalence to rewrite $x â‰¡B y$. Unfortunately,
we can not because a universe level matters, at least for now.
A first proof would have been saying $x â‰¡A y$ is a mere proposition and since
$A â‰ƒ B$, $x â‰¡B y$ is also a mere proposition. So, $B$ is a set.
Second proof is construct a term of 'isSet B' by using the inverse function
from the equivalence and some path algebra. Not happy with this but it works.


{: .foldable until="5"}
\begin{code}
  â‰ƒ-trans-inv
    : âˆ€ {â„“} {A B : Type â„“}
    â†’ (Î± : A â‰ƒ B)
    -----------------------------
    â†’ â‰ƒ-trans Î± (â‰ƒ-flip Î±) == Aâ‰ƒA

  â‰ƒ-trans-inv Î± = sameEqv (
    begin
      Ï€â‚ (â‰ƒ-trans Î± (â‰ƒ-sym Î±)) ==âŸ¨ refl _ âŸ©
      Ï€â‚ (â‰ƒ-sym Î±) âˆ˜ Ï€â‚ Î±     ==âŸ¨ funext (rlmap-inverse-h Î±) âŸ©
      id
    âˆ)
\end{code}

The following lemma is telling us, something we should probably knew already:
Equivalence of propositions is the same logical equivalence.

{: .foldable until="6"}
\begin{code}
  twoprops-to-equiv-â‰ƒ-â‡”
    : {A : Type â„“áµ¢} {B : Type â„“â±¼}
    â†’ isProp A
    â†’ isProp B
    -------------------
    â†’ (A â‰ƒ B) â‰ƒ (A â‡” B)

  twoprops-to-equiv-â‰ƒ-â‡” {A = A} {B} ispropA ispropB  = qinv-â‰ƒ f (g , Hâ‚ , Hâ‚‚)
   where
    f : (A â‰ƒ B) â†’ (A â‡” B)
    f e = e âˆ™â†’ , e âˆ™â†

    g : (A â‡” B) â†’ (A â‰ƒ B)
    g (hâ†’ , hâ†) = qinv-â‰ƒ hâ†’ (hâ† , (Î» b â†’ ispropB (hâ†’ (hâ† b)) b) , (Î» a â†’ ispropA (hâ† (hâ†’ a)) a))

    Hâ‚ : f âˆ˜ g âˆ¼ id
    Hâ‚ (hâ†’ , hâ†) = idp

    Hâ‚‚ : g âˆ˜ f âˆ¼ id
    Hâ‚‚ e =
      begin
        g (e âˆ™â†’ , e âˆ™â†)
          ==âŸ¨âŸ©
        e âˆ™â†’ , _
          ==âŸ¨ Î£-bycomponents (idp , isEquivIsProp (e âˆ™â†’) _ _) âŸ©
        e
      âˆ
\end{code}

FIXME : Put this somewhere else

{: .foldable until="6"}
\begin{code}
  âˆ‘-prop
    : {A : Type â„“áµ¢}{B : A â†’ Type â„“â±¼}
    â†’ isProp A
    â†’ ((a : A) â†’ isProp (B a))
    ------------------------
    â†’ isProp (âˆ‘ A B)

  âˆ‘-prop {B = B} iA Î»-iB u v
    = pair= (Î± , Î²)
    where
      Î± : Ï€â‚ u â‰¡ Ï€â‚ v
      Î± = iA (Ï€â‚ u) (Ï€â‚ v)

      Î² : (Ï€â‚‚ u) â‰¡ (Ï€â‚‚ v) [ B / Î± ]
      Î² = Î»-iB (Ï€â‚ v) (tr B Î± (Ï€â‚‚ u)) (Ï€â‚‚ v)
\end{code}

\begin{code}
  pi-is-set
    : {A : Type â„“áµ¢}{B : A â†’ Type â„“â±¼}
    â†’ ((a : A) â†’ isSet (B a))
    â†’ isSet (âˆ A B)

  pi-is-set  setBa f g = b
    where
    a : isProp (f âˆ¼ g)
    a h1 h2 = funext Î» x â†’ setBa x (f x) (g x) (h1 x) (h2 x)

    b : isProp (f â‰¡ g)
    b = isProp-â‰ƒ (â‰ƒ-sym eqFunExt) (pi-is-prop Î» a â†’ setBa a (f a) (g a))

  âˆ-set = pi-is-set
  Î -set = pi-is-set
\end{code}

The following was a custom version useful to deal with functions
with implicit parameters.

{: .foldable until="5" }
\begin{code}
  pi-is-prop-implicit
     : {A : Type â„“áµ¢}{B : A â†’ Type â„“â±¼}
     â†’ ((a : A) â†’ isProp (B a))
     --------------------------
     â†’ isProp ({a : A} â†’ B a)

  pi-is-prop-implicit {A = A} {B} f = isProp-â‰ƒ explicit-â‰ƒ-implicit (pi-is-prop f)
    where
     explicit-â‰ƒ-implicit
       : ((a : A) â†’ B a) â‰ƒ ({a : A} â†’ B a)
     explicit-â‰ƒ-implicit = qinv-â‰ƒ go ((Î» x xâ‚ â†’ x) , (Î» x â†’ idp) , (Î» x â†’ idp))
       where
         go : ((a : A) â†’ B a) â†’ ({a : A} â†’ B a)
         go f {a} = f a
\end{code}


\begin{code}
open HLevelLemmas public
\end{code}

{: .axiom }
\begin{code}
postulate
  law-excluded-middle
    : âˆ€ {â„“} {P : Type â„“}
    â†’ isProp P
    ------------
    â†’ P + (Â¬ P)

LEM = law-excluded-middle
\end{code}

and the more general propositions-as-types formulation of the
law of excluded middle is:

\begin{code}
postulate
 LEMâˆ
   : {A : Type â„“}
   â†’ A + (Â¬ A)
\end{code}


{: .foldable until="5"}
\begin{code}
law-double-negation
 : âˆ€ {â„“} {P : Type â„“}
 â†’ isProp P
 -----------
 â†’ (Â¬ (Â¬ P)) â†’ P

law-double-negation iP with LEM iP
law-double-negation iP | inl x = Î» _ â†’ x
law-double-negation iP | inr x = Î» pâ†’âŠ¥â†’âŠ¥ â†’ âŠ¥-elim (pâ†’âŠ¥â†’âŠ¥ x)
\end{code}

Law excluded middle and law of double negation are both equivalent.


Weak extensionality principle:

\begin{code}
WeakExtensionalityPrinciple
  : {A : Type â„“} {P : A â†’ Type â„“}
  â†’ ((x : A) â†’ isContr (P x))
  -------------------------
  â†’ isContr ( âˆ A P )

WeakExtensionalityPrinciple {A = A}{P} f =
  (fx , Î» h â†’  ! funext (Î» x â†’ ! (Ï€â‚‚ (f x)) (h x)))
  where
   fx : âˆ A P
   fx = Î» x â†’ Ï€â‚ (f x)
\end{code}

{: .hide }
\begin{code}
open import SigmaEquivalence
\end{code}

{: .foldable until="5" }
\begin{code}
isSet-Î£
  : {A : Type â„“áµ¢} â†’ {B : A â†’ Type â„“â±¼}
  â†’ isSet A â†’ ((a : A) â†’  isSet (B a))
  -------------------
  â†’ isSet (Î£ A B)

isSet-Î£ {A = A}{B} iA f x y
  = isProp-â‰ƒ
    (pair=Equiv {v = x}{y})
    (âˆ‘-prop (iA (Ï€â‚ x) (Ï€â‚ y))
      (Î» a â†’ f _ (tr (Î» x  â†’ B x) a (Ï€â‚‚ x)) (Ï€â‚‚ y) ))


sigma-is-set = isSet-Î£
âˆ‘-set = isSet-Î£
isSet-âˆ‘ = isSet-Î£
\end{code}


{ .foldable until="6" }
\begin{code}
â‰ƒ-is-set-from-sets
  : {A : Type â„“áµ¢}{B : Type â„“â±¼}
  â†’ isSet A
  â†’ isSet B
  --------------
  â†’ isSet (A â‰ƒ B)

â‰ƒ-is-set-from-sets {A = A}{B} ia ib
  = isSet-Î£ (pi-is-set  (Î» _ â†’ ib)) (Î» _ â†’ prop-is-set (isEquivIsProp _))
\end{code}

{: .foldable until="6" }
\begin{code}
â‰¡-is-set-from-sets
  : {A B : Type â„“áµ¢}
  â†’ isSet A
  â†’ isSet B
  --------------
  â†’ isSet (A â‰¡ B)

â‰¡-is-set-from-sets iA iB = equiv-with-a-set-is-set (â‰ƒ-sym eqvUnivalence) (â‰ƒ-is-set-from-sets iA iB)
\end{code}
